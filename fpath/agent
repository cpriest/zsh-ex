#!/bin/zsh
setopt NO_FUNCTION_ARGZERO LOCAL_OPTIONS;

() {
	setopt LOCAL_OPTIONS LOCAL_TRAPS;

	local SF=${(%):-%x};
	local SD=${SF:h};
	local SSH_ENV="$HOME/.ssh/agent-environment"

	source "$ZEX_DIR/lib/scripts.zsh" 2>/dev/null ||
		{ echo "Could not source zsh-ex/lib/scripts.zsh, is zsh-ex installed?"; exit 1; }

##																									\e[m
##	agent [options]
##		Loads active agent or starts agent and prompts for ssh key automatically.
##																									\e[m
##		\e[31;1moptions\e[m
##			-h		  Shows this help information
##			-v		  Increases verbosity, may be repeated multiple times
##

	# Parse the options passed in, this needs to match the options listed in the help file, see `man zshmodule` for details
	zparseopts -D -E v+=VERBOSITY h=HELP ||
		help;

	# Capture the verbosity number (number of -vvv's)
	VERBOSITY=${#VERBOSITY}

	# Display help if requested
	[[ "$HELP" ]] &&
		help;

	# Called on exit from this point forward
	on_exit() {

	}
	trap on_exit EXIT;

	# Command code starts here

	function start_agent() {
		echo "\e[38;5;51mStarted new SSH agent.\e[m";
		/usr/bin/ssh-agent | perl -pe 's/^echo.+$//' > "${SSH_ENV}";
		chmod 600 "${SSH_ENV}";
		source "${SSH_ENV}" > /dev/null;
	}

	function load_ssh_env() {
		source "${SSH_ENV}" >& /dev/null
	}

	function agent_running() {
		for ((cnt=0; cnt < 2; cnt++)) {
			agent_is_running &&
				break;
			sleep .25;		# If it's not running, give it 250ms
			load_ssh_env;	# Try and re-load the environment
		}
	}

	function agent_is_running() {
		#ps ${SSH_AGENT_PID} doesn't work under cywgin

		[[ -n "${SSH_AGENT_PID}" ]] &&
			ps -ef | \grep ${SSH_AGENT_PID} | \grep ssh-agent$ >& /dev/null
	}

	# Source SSH settings, if applicable

	if [ -f "${SSH_ENV}" ]; then
		lead_ssh_env;

		# If agent isn't running, start it.
		agent_running ||
			start_agent;
	else
		start_agent;
	fi

	# If agent is running, check that it has keys added
	agent_running && {
		ssh-add -L >& /dev/null  || {
			echo "\e[38;5;51mAdding ~/.ssh/id_rsa to ssh-agent.\e[m";
			/usr/bin/ssh-add ~/.ssh/id_rsa > /dev/null;
		}
	}

} "$@";
