#!/bin/zsh

() {
	setopt LOCAL_OPTIONS PIPE_FAIL;

	source ~/.config/zsh-ex/config;

	local SF=${(%):-%x};
	local SD=${SF:h};

	# TRAPINT() { return $(( 128 + $1 )); }
	# TRAPABRT() { return $(( 128 + $1 )); }

	help() {
		print -l "`cat $SF | \pcregrep -o1 '^##\t(.+)$'`";
		kill -ABRT $$;
	}

	error() {
		echo "\e[31;1m$@\e[m";
		help;
	}

	vmsg() {
		[[ $1 > $VERBOSITY ]] &&
			return;
		shift;	# Drop $1
		echo $*;
	}

	upscale_match() {
		UPSCALER="cat";
		pv UPSCALER_GLOBS;
		for x (${UPSCALER_GLOBS[@]}) {
			SPLIT=(${(s:=:)x});
			GLOB=${SPLIT[1]};

			[[ "$*" == ${~GLOB} ]] || 
				continue;

			UPSCALER="$ZSX_CFG/upscalers/${SPLIT[2]:t}";
			[[ -x "$UPSCALER" ]] ||
				error "An upscaler matched on $GLOB but $UPSCALER is not an executable file.";
			break;
		}
	}

	# Default Options
	_TAIL_N=(-n 50000);

	# Parse Options
	zparseopts -D -E -K v+=VERBOSITY h=HELP n:=_TAIL_N ||
		help;

	VERBOSITY=${#VERBOSITY}

	[[ "$HELP" != '' ]] &&
		help;

	# See if there is a match for the filepath in UPSCALE_GLOBS from zsh-ex/config
	upscale_match "$*";

	# Execute

	TAIL="tail";
	TAIL_OPTS=(-F --quiet ${_TAIL_N});

	FZF="fzf";
	FZF_OPTS=(--tac --layout=default --height=100% --no-sort --no-expect --exact --ansi --tabstop=4 --bind 'start:last' --bind 'change:first' --bind 'enter:first' --bind 'esc:cancel' --tail=500000);
	FZF_OPTS+=(--bind end:first+untrack-current)
	FZF_OPTS+=(--bind page-up:page-up+track-current)
	FZF_OPTS+=(--bind page-down:page-down+track-current)
	FZF_OPTS+=(--bind home:last+track-current)

	FZF_OPTS+=(--bind insert:untrack-current)
	FZF_OPTS+=(--bind delete:track-current)


	[[ -z "$*" ]] && {
		# Inline fzf mode
		$FZF $FZF_OPTS;     
		: 
	} || { 
		# We create a fifo to handle the pipe from tail to fzf so that when fzf exits, we can nuke the pipe and all other procs die off
		FIFO=$(mktemp -u); mkfifo "$FIFO"

		# We use stdbuf -oL to make sure tail is line buffered so that we get output in a timely manner
		{ stdbuf -oL $TAIL $TAIL_OPTS $* | $UPSCALER > "$FIFO" & FIFO_PID=$!; }

		
		$FZF $FZF_OPTS < "$FIFO"

		kill $FIFO_PID 2>/dev/null
		rm -f "$FIFO"
	}

} "$@";

##																									\e[m
##	tz [options] {file}
##		\e[1mTails the\e[m last -n lines from \e[1m{file}\e[m, following the file (-F) \e[1mand pipes it through the\e[m
##		first matching \e[1;38;5;6mupscaler\e[m (see zsh-ex/config UPSCALER_GLOBS), if any, \e[1mto fzf\e[m with some 
##		specific options for easily viewing & filtering lines of a file. 
##																									\e[m
##		\e[31;1mOptions\e[m
##																									\e[m
##			-h		  Shows this help information
##			-v		  Increases verbosity, may be repeated multiple times
##																									\e[m
##		\e[31;1mOptions passed to tail\e[m
##			-n \e[32;1m{num}\e[m 	Defaults to 50,000
##																									\e[m
##
